#Version: oct 2022

import glob
import re
import sys
from os.path import join

#########################################
## SCRIPT PARAMETERS - READ FROM CONFIG FILE 

# Directory with input fastq files and reference genome
DATADIR=config["DATADIR"]

# Reads file extension (FIRST PART OF FILENAME WILL BE USED AS SAMPLE ID)
ext1=config["ext1"]
ext2=config["ext2"]

# Reference genome file
refgen=config["refgen"]

length=config["length"]
threads=config["threads"]

# Consensus building parameters
# mincov = the minimum number of reads covering a site (otherwise: N)
mincov=config["mincov"]
# minfrac = If the second most frequent type is at least H fraction of the most common type then a heterozygous base type will be reported in the consensus
minfrac=config["minfrac"]
# minQ = minimum read mapping quality score (read is skipped if lower)
minQ=config["minQ"]
#########################################

SAMPLES, = glob_wildcards(DATADIR+"{sample}"+ext1)
RESDIR = "results/"

def message(txt):
	sys.stderr.write("+++++ " + txt + "\n")

def errormessage(txt):
	sys.stderr.write("ERROR " + txt + "\n")

NBSAMPLES = len(SAMPLES)

message(str(NBSAMPLES)+" samples will be mapped:")
for i in SAMPLES:
        message(str(i))

# Rules not sent on jobs
localrules: All 


# Rules
rule All:
	input:
		expand("Results/"+"{smp}"+"_consensus2.fasta",smp=SAMPLES),
		expand("Results/{smp}_mapping.stats",smp=SAMPLES),
		expand("Results/Intermediary_files/{smp}_mapping_to_cons_sort.bam",smp=SAMPLES),
		DATADIR+refgen+".bwt.2bit.64",

rule BWAindexRef:
	input:
		Ref=DATADIR+refgen
	output:
		DATADIR+refgen+".bwt.2bit.64"
	params:
		clust_log="/dev/null"
	conda:
		"Mapping_env.yaml"
	shell:
		"""
		echo "Indexing reference {input}"
		bwa-mem2 index {input}
		echo "Done !"
		"""

rule cleanReads:
	input:
		R1=DATADIR+"{smp}"+ext1,
                R2=DATADIR+"{smp}"+ext2,
	output:
		R1="Results/Intermediary_files/{smp}_R1_clean.fastq.gz",
		R2="Results/Intermediary_files/{smp}_R2_clean.fastq.gz"
	params:
		html="Results/Intermediary_files/{smp}_reads_cleaning.html",
		length=length,
		clust_log="Logs/{smp}_cleanReads.log"
	conda:
		"fastp_env.yaml"
	shell:
		"""
		fastp --in1 {input.R1} --in2 {input.R2} --out1 {output.R1} --out2 {output.R2} -l {params.length} -h {params.html}
		"""

rule BWAmemRef:
	input:
		R1=rules.cleanReads.output.R1,
		R2=rules.cleanReads.output.R2,
		Ref=rules.BWAindexRef.output,
		Refpath=DATADIR+refgen
	output:
		temp("Results/Intermediary_files/{smp}_mapping-to-ref.sam")
	params:
		clust_log="Logs/{smp}_mapping-to-ref.log",
		threads=threads
	conda:
		"Mapping_env.yaml"
	shell:
		"""
		echo "Round 1 assembly of {input.R1} on {input.Refpath}"
		bwa-mem2 mem -t {params.threads} -L 8,8 -B 2 {input.Refpath} {input.R1} {input.R2} > {output}
		echo "Done ! The output {output} of this rule will be removed and replaced by the sorted version."
		"""

rule samtoolsConsensus:
	input:
		rules.BWAmemRef.output
	output:
		cons="Results/Intermediary_files/{smp}_consensus1.fasta",
		tmpsort=temp("Results/Intermediary_files/{smp}_mapping_to_ref_sort.sam")
	params:
		clust_log="Logs/{smp}_consensus1.log",
	conda:
		"samtools_env.yaml"
	shell:
		"""
		echo "Sorting and consensus from Round 1 assembly"
		samtools sort {input} -O sam -o {output.tmpsort}
		samtools consensus -a -f fasta --show-del no {output.tmpsort} -o {output.cons}
		echo "Done !"
		"""

rule BWAindexCons:
	input:
		rules.samtoolsConsensus.output.cons
	output:
		"Results/Intermediary_files/{smp}_consensus1.fasta"+".bwt.2bit.64"
	params:
		clust_log="/dev/null"
	conda:
		"Mapping_env.yaml"
	shell:
		"""
		echo "Indexing consensus {input} to use it as reference for next assembly round"
		bwa-mem2 index {input}
		echo "Done !"
		"""

rule BWAmemCons:
	input:
		R1=rules.cleanReads.output.R1,
		R2=rules.cleanReads.output.R2,
		Ref=rules.BWAindexCons.output,
		Refpath=rules.samtoolsConsensus.output.cons
	output:
		"Results/Intermediary_files/{smp}_mapping_to_cons.sam"
	params:
		clust_log="Logs/{smp}_mapping-to-cons.log",
		threads=threads
	conda:
		"Mapping_env.yaml"
	shell:
		"""
		echo "Assembly of {input.R1} on {input.Refpath}"
		bwa-mem2 mem -t {params.threads} {input.Refpath} {input.R1} {input.R2} -T 51 > {output}
		echo "Done ! The output {output} of this rule will be removed and replaced by the sorted version."
		"""

rule samtoolsConsensus2:
	input:
		rules.BWAmemCons.output
	output:
		tmpsort=temp("Results/Intermediary_files/{smp}_mapping_to_cons_sort.sam"),
		outfile="Results/{smp}_consensus2.fasta"
	params:
		clust_log="Logs/{smp}_consensus2.log",
		seqID=">"+"{smp}",
		mincov=mincov,
		minfrac=minfrac,
		minQ=minQ
	conda:
		"samtools_env.yaml"
	shell:
		"""
		echo "Sorting and consensus from Round 2 assembly"
		samtools sort {input} -O sam -o {output.tmpsort}
		samtools consensus -m "simple" -A -d {params.mincov} -c 0.5 -H {params.minfrac} --min-MQ {params.minQ} -f fasta --show-del no {output.tmpsort} -o {output.outfile}
		sed -i "s/>.*$/{params.seqID}/" {output.outfile}
		echo "Done !"
		"""

rule assemblyStats:
	input:
		rules.samtoolsConsensus2.output.tmpsort
	output:
		"Results/{smp}_mapping.stats"
	params:
		clust_log="Logs/{smp}_assemblyStats.log"
	conda:
		"samtools_env.yaml"
	shell:
		"""
		samtools coverage {input} > {output}
		"""

rule sam2bam:
	input:
		S2=rules.samtoolsConsensus2.output.tmpsort,
		S1=rules.samtoolsConsensus.output.tmpsort
	output:
		B1="Results/Intermediary_files/{smp}_mapping_to_ref_sort.bam",
		B2="Results/Intermediary_files/{smp}_mapping_to_cons_sort.bam"
	params:
		clust_log="Logs/{smp}_sam2bam.log"
	conda:
		"samtools_env.yaml"
	shell:
		"""
		samtools view -S -b {input.S1} > {output.B1}
		samtools view -S -b {input.S2} > {output.B2}
		"""


